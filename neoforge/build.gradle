plugins {
    id "com.gradleup.shadow"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    implementation("icyllis.modernui:ModernUI-NeoForge:${rootProject.minecraft_version}-${rootProject.modernui_version}.+")
    modImplementation("icyllis.modernui:ModernUI-Core:${modernui_version}") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.jetbrains", module: "annotations"
        exclude group: "com.ibm.icu", module: "icu4j"
        exclude group: "it.unimi.dsi", module: "fastutil"
    }

    modImplementation("icyllis.modernui:ModernUI-Markflow:${modernui_version}") {
        exclude group: "org.slf4j", module: "slf4j-api"
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: "com.google.code.findbugs", module: "jsr305"
        exclude group: "org.jetbrains", module: "annotations"
        exclude group: "com.ibm.icu", module: "icu4j"
        exclude group: "it.unimi.dsi", module: "fastutil"
    }

    implementation "io.github.classgraph:classgraph:$rootProject.classgraph_version"
    shadowBundle "io.github.classgraph:classgraph:$rootProject.classgraph_version"

    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$rootProject.jackson_version"
    shadowBundle "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$rootProject.jackson_version"

    implementation "com.github.umjammer:jlayer:$rootProject.jlayer_version"
    shadowBundle "com.github.umjammer:jlayer:$rootProject.jlayer_version"

    implementation "org.jflac:jflac-codec:$rootProject.jflac_version"
    shadowBundle "org.jflac:jflac-codec:$rootProject.jflac_version"
}
processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    zip64 = true
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
}


loom {
    runs {
        client {
            runDir = "run/client"
        }
        server {
            runDir = "run/server"
        }
    }
}